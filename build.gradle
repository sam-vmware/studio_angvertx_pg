configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()

        // force certain versions of dependencies (including transitive)
        //  *append new forced modules:
        force 'org.codehaus.groovy:groovy-all:2.2.1'
        //  *replace existing forced modules with new ones:
        forcedModules = ['org.codehaus.groovy:groovy-all:2.2.1']

        // add a dependency resolve rule
        eachDependency { DependencyResolveDetails details ->
            //changing 'groovy-all' into 'groovy':
            if (details.requested.name == 'groovy-all') {
                //details.useTarget group: details.requested.group, name: 'groovy', version: details.requested.version
                details.useTarget group: details.requested.group, name: 'groovy-all', version: '2.2.1'
            }
        }

        // cache dynamic versions for 10 minutes
        cacheDynamicVersionsFor 10*60, 'seconds'
        // don't cache changing modules at all
        cacheChangingModulesFor 0, 'seconds'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

// Some global conf to share around projects
ext.VMWARE_CONF = [
    groovyVersion: "2.2.1",
    groovyConsolePath: "/usr/local/java/groovy/groovy-2.2.1/bin/groovyConsole",
    vertx: [
        // Change this to the lib directory of where your Vert.x build lib directory is
        libDir: "/home/samueldoyle/Projects/GitHub/Vertx/vertx/build/current/lib",
        // Additional jar dependencies
        // !! IMPORTANT !!
        // The vertx lang-groovy-xxx*.jar is in here and MUST MATCH the
        // the mod version you use if you are doing a custom vertx build.
        // this is the version that gets placed in your $VERTX_MODS directory
        addedLibs: "$rootDir/shared/common/added_libs",
        groovyLangModVersion: "2.1M6-SNAPSHOT"
    ]
]

allprojects {
    //apply plugin: 'java'
    apply plugin: 'groovy'
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
    group = 'com.vmware.studio'
    version = '1.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.2.1'
    }
}

subprojects {
}

task(console, dependsOn: 'classes', type:Exec) {
    def cp = sourceSets.main.runtimeClasspath.filter {
        ! it.name.contains("groovy-all")
    }
    cp += files("$rootDir/classes/production/common")

    def jars =
        files(fileTree(dir: VMWARE_CONF.vertx.libDir, includes: ['*.jar'])) +
            files(fileTree(dir: VMWARE_CONF.vertx.addedLibs, includes: ['*.jar']))
    environment["JARS_TO_ADD"] = jars.getAsPath()

    def consoleCP = cp + jars
    //commandLine '/usr/bin/env', 'groovyConsole', '--classpath', cp.getAsPath()
    commandLine "${VMWARE_CONF.groovyConsolePath}", '--classpath', consoleCP.getAsPath()
    ignoreExitValue true
}

